Name     Serial Receiver;
PartNo   00 ;
Date     27/03/2015 ;
Revision 01 ;
Designer Engineer ;
Company  CCISEL ;	
Assembly None ;
Location  ;
Device   v750c ;

/* *************** INPUT PINS *********************/
PIN 1 = MCLK;
PIN 2 = MICK;
PIN 3 = MIXD;
PIN 4 = LnP;
PIN 5 = AnF;
PIN 6 = PA;
PIN 7 = PF;

/* *************** OUTPUT PINS ********************/
PIN 14 = DXVAL;
PIN 15 = MIBy;
PIN 16 = WrL;
PIN 17 = AP;
PIN 18 = FP;
PIN [20..23] = [ADDRESS0..3];

/* *************** Pin nodes *****************/
PINNODE [27..28]  = [MSTART0..1];
PINNODE [29..30]  = [MCONTROLO0..1];
PINNODE [31..32] = [MDISPATCHER0..1];

/* ************** MODULO START ********************/
[MSTART0..1].sp = 'b'0;
[MSTART0..1].ar = 'b'0;
[MSTART0..1].ck = MCLK;

/* *************** ESTADOS *****************/

Sequence [MSTART1..0]{

Present 'b'00
	if !MICK & MIXD next 'b'01 ;
	default next 'b'00 ;
Present 'b'01
	if !MICK & !MIXD next 'b'10 ;
	if MICK next 'b'00 ;
	default next 'b'01;
Present 'b'10
	out START;
	default next 'b'00;
}

/* *************** Modulo Contador *****************/
[ADDRESS0..3].sp = 'b'0;
[ADDRESS0..3].ar = START;
[ADDRESS0..3].ck = !MICK;


ADDRESS0.d = 'b'1 $ ADDRESS0 ;
ADDRESS1.d = ADDRESS0 $ ADDRESS1 ;
ADDRESS2.d = ADDRESS0 & ADDRESS1 $ ADDRESS2 ;
ADDRESS3.d = ADDRESS0 & ADDRESS1 & ADDRESS2 $ ADDRESS3 ;


/* ************** MODULO CONTROLADOR ********************/
[MCONTROLO0..1].sp = 'b'0;
[MCONTROLO0..1].ar = START;
[MCONTROLO0..1].ck = MCLK;

EQUAL9 = ADDRESS3 & !ADDRESS2 & !ADDRESS1 & ADDRESS0;
EQUAL3 = !ADDRESS3 & !ADDRESS2 & ADDRESS1 & ADDRESS0;

/* *************** ESTADOS *****************/

Sequence [MCONTROLO1..0]{

Present 'b'00
	if MICK & LnP & EQUAL9 # MICK & !LnP & EQUAL3 next 'b'01 ;
	default next 'b'00 ;
Present 'b'01
	out DXVAL;
	default next 'b'10;
Present 'b'10
	if !ACCEPT out MIBy;
	default next 'b'10;
}

/* ************** MODULO DISPATCHER ********************/
[MDISPATCHER0..1].sp = 'b'0;
[MDISPATCHER0..1].ar = 'b'0;
[MDISPATCHER0..1].ck = MCLK;

/* *************** ESTADOS *****************/

Sequence [MDISPATCHER1..0]{

Present 'b'00
	out ACCEPT;
	if DXVAL & LnP next 'b'01;
	if DXVAL & !LnP & AnF & !PA next 'b'10;
	if DXVAL & !LnP & !AnF & !PF next 'b'11;
	default next 'b'00;
Present 'b'01
	out WrL;
	default next 'b'00;	
Present 'b'10
	out AP;
	if PA next 'b'00;
	default next 'b'10;	
Present 'b'11
	out FP;
	if PF next 'b'00;
	default next 'b'11;	
	
}